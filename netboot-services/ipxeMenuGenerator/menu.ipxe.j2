#!ipxe

:start

# Language-Detection: Set different languages based on the default gateway IP address
:language
# Lausanne
iseq ${netX/gateway} 172.20.72.1 && set language fr_CH && goto macboot ||
# Genf
iseq ${netX/gateway} 172.20.56.1 && set language fr_CH && goto macboot ||
# Krefeld
iseq ${netX/gateway} 172.22.32.1 && set language de_DE && goto macboot ||
# Odilia
iseq ${netX/gateway} 172.22.224.1 && set language de_DE && goto macboot ||
# Fallback (when no condition above triggers to set next-server)
set language de_CH

:macboot
chain --autofree tftp://{{ netbootServerIP }}/MAC-${mac:hexraw}.ipxe || echo Custom boot by MAC not found, going to menu...

:initial_menu
set sp:hex 20 && set sp ${sp:string}
menu DG-Network-Bootloader
item --gap Standard:
item dg-thinclient-prod ${sp} DG ThinClient
item --gap Erweitert:
item advanced ${sp} Erweiterte Bootoptionen
item reboot ${sp} Neustart
item --gap Aktuell gesetzter Bootserver: {{ netbootServerIP }}
item --gap Aktuell gesetzte Sprache: ${language}
choose --timeout 10000 initial_choice || goto start
goto ${initial_choice}

# Bootconfigurtion for our netboot-OS
:dg-thinclient-prod
set squash_url http://{{ netbootServerIP }}/prod/{{ imageName }}.squashfs
set kernel_url http://{{ netbootServerIP }}/kernels/{{ kernelFolderName }}/

:startboot
imgfree
kernel ${kernel_url}vmlinuz ip=dhcp boot=casper netboot=url url=${squash_url} initrd=initrd locale=${language} ${cmdline}
initrd ${kernel_url}initrd
boot

# Chaining the advanced menu.
:advanced
chain --autofree tftp://{{ netbootServerIP }}/advancedmenu.ipxe

:localboot
exit

:retry
goto start

:reboot
reboot
goto start

:debug
echo Type "exit" to return to menu
shell
goto start

